# http://www.wikidata.org/entity/Q54554025
# citation needed constraint (Q54554025) specifies that statements for a certain property should have at least one reference.
# if ?s ?p ?o exists, where ?p = p:PID, then the statement node ?o must have at least one reference (checked with prov:wasDerivedFrom).
# e.g. ?p = academic degree (P512)
SELECT
?subject
?wdt_prop
?constraint_status
?deprecated_reason
WHERE
{
  ?subject ?p_property ?stmt.
  # violations are statements without a reference...
  FILTER NOT EXISTS {?stmt prov:wasDerivedFrom ?wdref.}
  # Alternative: Strictly speaking also statemens with an empty reference should be reported, so this could be further refined to ...
  # FILTER NOT EXISTS {?stmt prov:wasDerivedFrom ?wdref. FILTER( ! isBlank(?wdref) }
  # ... but we refrained from this formulations because isBlank is not supported in Wikidata's query endpoint, cf. https://www.mediawiki.org/wiki/Wikidata_Query_Service/Blank_Node_Skolemization
  ?wd_property wikibase:claim ?p_property.
  ?wd_property wikibase:directClaim ?wdt_prop.
  ?wd_property p:P2302 ?statement.
  ?statement ps:P2302 wd:Q54554025. ## citation needed constraint 
  OPTIONAL {?statement pq:P2316 ?constraint_status}.
  OPTIONAL {?statement pq:P2241 ?deprecated_reason}.
  
  # EXAMPLE (comment next line clause to query all violations for all properties of this type...)
  # ... however note the risk of timeout: we suggest to run these queries one by one for each property of this type
  FILTER (?wdt_prop = wdt:P512)
} 
