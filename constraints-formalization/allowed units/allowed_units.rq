# http://www.wikidata.org/entity/Q21514353
# type of constraint for Wikidata properties: used to specify that only listed units may be used
# if ?s ?p ?o exists and ?p has allowed units constraint, then the units used in the value must match the ones allowed by the constraint
# e.g. for ?p = wind speed (P5065), all values must be in metre per second (Q182429)
SELECT DISTINCT 
  ?subject
  ?p_property
  ?unit
  ?constraint_status
  ?deprecated_reason
WHERE
{
    ?subject ?p_property [?psv_property ?psv_stmt] .
    ?psv_stmt wikibase:quantityUnit ?unit.
  
    ?property wikibase:claim ?p_property.
    ?property wikibase:statementValue ?psv_property.
    ?property p:P2302 ?statement.
    ?statement ps:P2302 wd:Q21514353 . ## allowed units constraint
  
    
    FILTER NOT EXISTS {?statement pq:P2305 ?unit}
    FILTER NOT EXISTS {?statement pq:P2303 ?subject. }
    OPTIONAL {?statement pq:P2316 ?constraint_status}.
    OPTIONAL {?statement pq:P2241 ?deprecated_reason}.
  
  # EXAMPLE (comment next line to query all violations for all properties of this type...)
  # ... however note the risk of timeout: we suggest to run these queries one by one for each property of this type
  FILTER (?p_property = p:P5065)
}
