# type of constraint for Wikidata properties: used to specify that a property generally contains more than one value per item
# if ?s ?p ?o exists and ?p has multi-value constraint, then ?s ?p ?o2 with ?o2 != ?o should also exist
# if ?p = primary destinations (P1302), multiple values are expected
SELECT DISTINCT 
?subject
?wdt_property
?object
?constraint_status
?deprecated_reason
WHERE
{
  ?subject ?wdt_property ?object.
  FILTER NOT EXISTS {
    ?subject ?wdt_property ?o2.
    FILTER (?object != ?o2)
  }
  ?property wikibase:directClaim ?wdt_property .
  ?property p:P2302 ?statement .
  ?statement ps:P2302 wd:Q21510857 . ## multi-value constraint
  # including exceptions
  #OPTIONAL {?statement pq:P2303 ?exception}.
  OPTIONAL {?statement pq:P2316 ?constraint_status}.
  # collecting deprecated ones
  OPTIONAL {?statement pq:P2241 ?deprecated_reason}.
  
  # EXAMPLE (comment next line to query all violations for all properties of this type...)
  # ... however note the risk of timeout: we suggest to run these queries one by one for each property of this type
  FILTER (?wdt_property = wdt:P1302)
}