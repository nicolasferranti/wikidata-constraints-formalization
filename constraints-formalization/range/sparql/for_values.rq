# type of constraint for Wikidata properties: used to specify that the value must be between two given values
# if ?s ?p ?o exists, then the literal value ?o must be in range [?minimum,?maximum]
# e.g. if ?p = density (P2054), ?o must be higher than minimum value (P2313)
# and less than maximum value (P2312)
# SPARQL representation:
SELECT
?subject ?wdt_property ?object
?minimum ?maximum
{
  ?subject ?wdt_property ?object.
  FILTER (?object < ?minimum || ?object > ?maximum)
  ?property wikibase:directClaim ?wdt_property.
  ?property p:P2302 ?statement.
  ?statement ps:P2302 wd:Q21510860. ## range constraint 
  ?statement pq:P2313 ?minimum. # value
  ?statement pq:P2312 ?maximum. 
  # including exceptions
  OPTIONAL {?statement pq:P2303 ?exception}.
  OPTIONAL {?statement pq:P2316 ?constraint_status}.
  # getting deprecated ones
  OPTIONAL {?statement pq:P2241 ?deprecated_reason}.

  # EXAMPLE (comment next line to query all violations for all properties of this type...)
  # ... however note the risk of timeout: we suggest to run these queries one by one for each property of this type
  FILTER (?wdt_property = wdt:P2054)
} 