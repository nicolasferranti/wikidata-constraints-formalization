# property constraint for restricting the use of a property to lexemes in a particular language
# is ?s ?p ?o exists and ?p has lexeme requires LANGUAGE constraint, then the subject ?s should have a particular language
# if ?p = Elhuyar Dictionary ID (P6838), lexemes such as urtarril (https://www.wikidata.org/wiki/Lexeme:L8720) must have a specific language
SELECT
?subject ?wdt_property ?object ?lang
{
  ?wd_property p:P2302 ?statement.
  ?wd_property wikibase:directClaim ?wdt_property.
  ?statement ps:P2302 wd:Q55819106. # lexeme requires LANGUAGE constraint 
  
  ?subject ?wdt_property ?object.
  ?subject dct:language ?lang.
  FILTER NOT EXISTS {?statement pq:P2305 ?lang.}
  
  # EXAMPLE (comment next line to query all violations for all properties of this type...)
  # ... however note the risk of timeout: we suggest to run these queries one by one for each property of this type
  FILTER (?wdt_property = wdt:P6838)
}