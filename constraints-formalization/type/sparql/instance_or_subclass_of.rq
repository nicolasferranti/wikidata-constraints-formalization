# type of constraint for Wikidata properties: used to specify that the item described by such properties should be a subclass or instance of a given type
# The subjects of a statement ?s ?p ?o must be an instance or subclass of one of the listed classes
# e.g. if ?p = OpenStreetMap element (P10689), then ?s must be an instance or subclass of geographical feature,
# geographic entity, hotel, work of art, or educational institution
# SPARQL representation:
SELECT DISTINCT 
    ?subject
    ?wdt_property 
    ?object
    ?statement
WHERE
{  
  ?wd_property wikibase:directClaim ?wdt_property.
  ?wd_property p:P2302 ?statement.
  ?statement ps:P2302 wd:Q21503250. ## type constraint
  ?statement pq:P2309 wd:Q30208840. # instance or subclass-of    

  ?subject ?wdt_property ?object.

  FILTER NOT EXISTS {
    ?statement pq:P2308 ?allowed_class.
    ?subject (wdt:P31/wdt:P279*)|wdt:P279* ?allowed_class.
  }

  # EXAMPLE (comment next line to query all violations for all properties of this type...)
  # ... however note the risk of timeout: we suggest to run these queries one by one for each property of this type
  FILTER (?wdt_property = wdt:P10689)
}