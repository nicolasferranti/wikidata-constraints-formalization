# type of constraint for Wikidata properties: used to specify that the referenced lexeme should have a given lexical category
# is ?s ?p ?o exists and ?p has lexeme value requires lexical category constraint, then the object ?o must have a lexical category
# if ?p = personal pronoun (P6553), lexemes such as she (https://www.wikidata.org/wiki/Lexeme:L484) must have a lexical category
SELECT DISTINCT
?subject ?wdt_property ?object
{
  ?wd_property p:P2302 ?statement.
  ?wd_property wikibase:directClaim ?wdt_property.
  ?statement ps:P2302 wd:Q64006792. #lexeme value requires lexical category constraint
  
  ?subject ?wdt_property ?object. 
  ?object wikibase:lexicalCategory ?lex_cat.
  FILTER NOT EXISTS {
    ?statement pq:P2305 ?expected_item.
    ?lex_cat wdt:P279* ?expected_item.
  }
  
  # EXAMPLE (comment next line to query all violations for all properties of this type...)
  # ... however note the risk of timeout: we suggest to run these queries one by one for each property of this type
  FILTER (?wdt_property = wdt:P6553)
}