# type of constraint for Wikidata properties: used to specify that the referenced item should have a statement with a given property
# The objects (?o) of a statement ?s ?p ?o must have a specific statement ?o ?wdt_required_property ?any_required_value
# e.g. if ?p = storyboard artist (P3275), then ?wdt_required_property = occupation and
# ?any_required_value = storyboard artist, artist, story artist, illustrator, character designer, concept artist,
# animator, or character animator
# SPARQL representation:
SELECT DISTINCT
  ?o
  ?wdt_property 
  ?wdt_required_property
  ?required_value
{
    {
      ?property p:P2302 ?statement.
      ?property wikibase:directClaim ?wdt_property.
      ?statement ps:P2302 wd:Q21510864. ## value-requires-statement constraint
      ?statement pq:P2305 ?has_required_value.
      ?statement pq:P2306 ?required_property.
      ?required_property wikibase:directClaim ?wdt_required_property.
        
      ?s ?wdt_property ?o.
    }
      
    FILTER NOT EXISTS { 
      ?o ?wdt_required_property ?any_required_value. 
      ?statement pq:P2305 ?any_required_value.
    }
  
  # EXAMPLE (comment next line to query all violations for all properties of this type...)
  # ... however note the risk of timeout: we suggest to run these queries one by one for each property of this type
  FILTER (?wdt_property = wdt:P3275)
}