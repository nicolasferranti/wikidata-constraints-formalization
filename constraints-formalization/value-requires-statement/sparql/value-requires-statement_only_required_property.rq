# type of constraint for Wikidata properties: used to specify that the referenced item should have a statement with a given property
# The objects (?o) of a statement ?s ?p ?o must have a specific statement ?o ?wdt_required_property []
# e.g. if ?p = interchange station (P833), then ?wdt_required_property = connecting line
# SPARQL representation:
SELECT 
  ?s ?o
  ?wdt_property 
  ?wdt_required_property
  ?required_value
{
  {
    ?property p:P2302 ?statement.
    ?statement ps:P2302 wd:Q21510864. ## value-requires-statement constraint
    ?statement pq:P2306 ?any_required_property.
    
    ?property wikibase:directClaim ?wdt_property.
    
    # REQUIRED PROPERTY ONLY, NO VALUE
    FILTER NOT EXISTS {?statement pq:P2305 []}
        
    ?s ?wdt_property ?o.
  }
        
  FILTER NOT EXISTS 
  {
    ?statement pq:P2306 ?required_property.
    ?required_property wikibase:directClaim ?wdt_required_property.
    ?o ?wdt_required_property [].
  }
  # EXAMPLE (comment next line to query all violations for all properties of this type...)
  # ... however note the risk of timeout: we suggest to run these queries one by one for each property of this type
  FILTER (?wdt_property = wdt:P833)
}