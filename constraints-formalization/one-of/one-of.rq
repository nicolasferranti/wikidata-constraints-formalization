## type of constraint for Wikidata properties: used to specify that the value for this property has to be one of a given set of items
# if ?s ?p ?o exists and ?p has one-of constraint, then ?o must be one of the listed values
# if ?p = canonization status (P411), ?o must be saint, blessed, roman catholic saint, or  venerable
SELECT DISTINCT 
?subject ?property ?object #oneof-constraint violation
?constraint_status ?deprecated_reason
WHERE
{
  ?property wikibase:directClaim ?actualProperty.
  ?property p:P2302 ?statement.
  ?statement ps:P2302 wd:Q21510859. ## one-of
  ?subject ?actualProperty ?object.
  FILTER NOT EXISTS {?statement pq:P2305 ?object.}
  
  OPTIONAL {?statement pq:P2316 ?constraint_status}.
  OPTIONAL {?statement pq:P2241 ?deprecated_reason}.
  # EXAMPLE (comment next line to query all violations for all properties of this type...)
  # ... however note the risk of timeout: we suggest to run these queries one by one for each property of this type
  FILTER (?actualProperty = wdt:P411)
}