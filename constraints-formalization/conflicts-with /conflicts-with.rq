# https://www.wikidata.org/wiki/Q21502838
# type of constraint for Wikidata properties: used to specify that an item must not have a given statement
# if ?s ?p ?o exists and ?p has conflicts-with constraint, then the subject ?s must note have a given statement, in another words, ?s ?target_property ?entity should not exist
# e.g. ?p = Apple Podcasts podcast ID (P5842), subjects ?s must not be an istance of (P31) radio program (https://www.wikidata.org/wiki/Q1555508) or radio series (https://www.wikidata.org/wiki/Q14623351).
# Radio programs / show and podcasts based on said programs should be seperate items
SELECT
?subject
?property # main property
?target_property # conflitant property
?entity # optional conflitant object 
?replacement_property
?replacement_value
?deprecated_reason
?constraint_status
WHERE
{
  ?property wikibase:directClaim ?wdt_prop.
  ?property p:P2302 ?statement.
  ?statement ps:P2302 wd:Q21502838. ## conflicts-with constraint
  ?statement pq:P2306/wikibase:directClaim ?target_property .
  OPTIONAL {?statement pq:P2305 ?entity}.
  
  ?subject ?wdt_prop ?object.
  ?subject ?target_property ?entity.
  
  #replacement data, for analysis
  OPTIONAL {?statement pq:P6824 ?replacement_property}.
  OPTIONAL {?statement pq:P9729 ?replacement_value}.
    
  OPTIONAL {?statement pq:P2316 ?constraint_status}.
  OPTIONAL {?statement pq:P2241 ?deprecated_reason}.
  FILTER NOT EXISTS { ?statement pq:P2303 ?subject.}
  
  # EXAMPLE (comment next line clause to query all violations for all properties of this type...)
  # ... however note the risk of timeout: we suggest to run these queries one by one for each property of this type
  FILTER (?wdt_prop = wdt:P5842)
}