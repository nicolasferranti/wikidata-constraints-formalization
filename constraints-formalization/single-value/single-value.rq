# type of constraint for Wikidata properties: used to specify that this property generally contains a single value per item
# if ?s ?p ?o exists, then the graph can not have ?s ?p ?o2 and ?o2 != ?o
# The only exception is when a separator property is defined and both ?o and ?o2 have different values for the separator
# e.g. if ?p = DOAB publisher ID (P5062), as an ID, each ?s should have only one ?o
# e.g. if ?p = capital (P36), ?o != ?o2 is allowed as long as they have a different point in time (P585)
# SPARQL representation:
select DISTINCT
  ?subject
  ?wd_property
  ?wds1 ?wds2
  ?o1_entity ?o2_entity
  ?constraint_status ?deprecated_reason
{
  
  ?wd_property wikibase:statementProperty ?ps_property.
  ?wd_property p:P2302 ?statement.
  ?statement ps:P2302 wd:Q19474404. # single-value constraint
  
  ?wd_property wikibase:claim ?p_property.
  
  # Q5782830 same value for separators
  # Q154816 no separators
  ?subject ?p_property ?wds1,?wds2.
  
  ?wds1 ?ps_property ?o1_entity. 
  ?wds2 ?ps_property ?o2_entity.
  
  FILTER (str(?o1_entity) < str(?o2_entity))
  FILTER (
    NOT EXISTS {
      ?statement pq:P4155/wikibase:qualifier ?pq_sep.
      ?wds1 ?pq_sep [].
      ?wds2 ?pq_sep [].
    }
    ||
    NOT EXISTS {
      # example P36 (capital)
      # if it has a separator
      ?statement pq:P4155/wikibase:qualifier ?separator_property_qualifier.
      ?wds1 ?separator_property_qualifier ?o1_qualifier.
      ?wds2 ?separator_property_qualifier ?o2_qualifier.
      FILTER (?o1_qualifier != ?o2_qualifier)
    }
  )
  
  
  OPTIONAL {?statement pq:P2316 ?constraint_status}.
  OPTIONAL {?statement pq:P2241 ?deprecated_reason}.  
  
  # EXAMPLE (comment next line to query all violations for all properties of this type...)
  # ... however note the risk of timeout: we suggest to run these queries one by one for each property of this type
  FILTER (?p_property = p:P36)
}
